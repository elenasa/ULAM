## -*- mode:ulam -*-
#=t3759_test_compiler_elementinheritedquark_virtualfuncuser_wrefstorefs
##
## gen output: (4 asserts) requires quark vtable support
##
## eval fails on 2 quark refs (cases 3 and 4)
#!
Exit status: 0
Uq_Base { <NOMAIN> }
Uq_Sub1 : Base { :Base<> <NOMAIN> }
Ue_E1 : Base { :Base<> <NOMAIN> }
Ue_E2 : Sub1 { :Sub1<> ^Base<> <NOMAIN> }
Ue_Test : System { Bool(1) brtn[4](true,true,false,false);  :System<> Int(32) test() {  E1 e1;  E1& e1ref = e1 cast;  Base& bref1 = e1ref cast;  self brtn . 2 [] bref1 ( )vuser . 7 cast == = self ( self brtn . 2 [] )assert . E2 e2;  E2& e2ref = e2 cast;  Base& bref2 = e2ref cast;  self brtn . 3 [] bref2 ( )vuser . 9 cast == = self ( self brtn . 3 [] )assert . Base b;  Base& bref = b cast;  Base& bref3 = bref;  self brtn . 0 [] bref3 ( )vuser . 2 cast == = self ( self brtn . 0 [] )assert . Sub1 s1;  Sub1& s1ref = s1 cast;  Base& bref4 = s1ref cast;  self brtn . 1 [] bref4 ( )vuser . 2 cast == = self ( self brtn . 1 [] )assert . 0 cast return } }
Uq_System { <NOMAIN> }
##
## informed by 3758, 3602, vtable-example 11/19/15
##
#:Base.ulam
  ulam 2;
quark Base {
  Int afunc() { return 1; } // not virtual
  virtual Int afunc(Int a) { return 2; } // but overload of same name can be virtual
  virtual Int bfunc() { return 3; }
  Int vuser() {
    return afunc(0); // virtual function invocation: Might return 2, 7, or 9 in this example!
  }
}

#:Sub1.ulam
  ulam 2;
use Base;
quark Sub1 : Base {
  Int afunc() { return 4; } // not virtual
  virtual Int bfunc() { return 5; } // virtual override
  virtual Int cfunc() { return 6; } // new func
}

#:E1.ulam
  ulam 2;
use Base;
element E1 : Base {
  virtual Int afunc(Int a) { return 7; } // virtual override
  virtual Int bfunc() { return 8; } // virtual override
}

#:E2.ulam
  ulam 2;
use Sub1;
element E2 : Sub1 {
  virtual Int afunc(Int a) { return 9; }
  virtual Int cfunc() { return 10; }
}

#>Test.ulam
  ulam 2;
use Base;
use Sub1;
use E1;
use E2;
use System;
element Test : System {
  Bool brtn[4];

  Int test() {

    E1 e1;
    E1 & e1ref = e1;
    Base& bref1 = e1ref;
    brtn[2] = (bref1.vuser() == 7);
    assert(brtn[2]);

    E2 e2;
    E2 & e2ref = e2;
    Base& bref2 = e2ref;
    brtn[3] = (bref2.vuser() == 9);
    assert(brtn[3]);

    //       requires immediate quark vtable support
    Base b;
    Base& bref = b;
    Base& bref3 = bref;
    brtn[0] = (bref3.vuser() == 2);
    assert(brtn[0]);

    //       requires immediate quark vtable support
    Sub1 s1;
    Sub1 & s1ref = s1;
    Base& bref4 = s1ref; //unused var (s1ref read)
    brtn[1] = (bref4.vuser() == 2);
    assert(brtn[1]);

    return 0;
  }
}

#:System.ulam
  ulam 1;
quark System {
  Void print(Unsigned arg) native;
  Void print(Int arg) native;
  Void print(Int(4) arg) native;
  Void print(Int(3) arg) native;
  Void print(Unary(3) arg) native;
  Void print(Bool(3) arg) native;
  Void assert(Bool b) native;
}
#.
